class Controller is subclass of GLOBAL
types
	public ControllerMode = <Controller> | <SparePart>;
	
	private ControllerState = <Idle> |
		<SearchingForKeyVault> | 
		<WaitingToJoin> |
		<WaitingForChallengeSolvedAcknowledgement> |
		<WaitingForChallengeSolvedReply> |
		<WaitingForNewEffectiveCertificate> |
		<Connected> |
		<WaitingForNewEffectiveCertificateDuringRekeying> |
		<Undefined>;
	
values
	private sparePartStatesSequence: seq of ControllerState = [
		<Idle>,
		<SearchingForKeyVault>,
		<WaitingToJoin>,
		<WaitingForChallengeSolvedAcknowledgement>,
		<WaitingForChallengeSolvedReply>,
		<WaitingForNewEffectiveCertificate>,
		<Connected>
	];

	private controllerStatesSequence: seq of ControllerState = [
		<Connected>,
		<WaitingForNewEffectiveCertificateDuringRekeying>
	];


instance variables
	private state: ControllerState :=  <Idle>;
	public id: token;
	public ipAddress: token;
	private mode: ControllerMode := <SparePart>;
	
	inv state <> undefinedState;
	
operations
	public Controller: token * token ==> Controller
	Controller(newId, newIpAddress) == (
		id := newId;
		ipAddress := newIpAddress;
	);
	
	public NextState: Message ==> ()
	NextState(message) == (
		cases message.getMessageType():
			"SendBroadcastToSearchKeyVault" -> state := <SearchingForKeyVault>,
			"JoinRequest" -> state := <WaitingToJoin>,
			"SendChallengeReply" -> state := <WaitingForChallengeSolvedAcknowledgement>,
			"SendChallenge" -> state := <WaitingForChallengeSolvedReply>,
			"SendNewEffectivePublicKey" -> state := <WaitingForNewEffectiveCertificate>,
			"SendNewEffectiveCertificateAcknoledgement" -> state := <Connected>,
			"SendNewEffectivePublicKeyWithSignature" -> state := <WaitingForNewEffectiveCertificateDuringRekeying>,
			others -> state := undefinedState
		end;
	)
	pre id = message.getControllerId()
	post (
		if mode = <SparePart> then 
			state = GetNextStateFromSequenceUsingCurrentState[ControllerState](sparePartStatesSequence, state~) -- state~ is the previous state
		else 
			state = GetNextStateFromSequenceUsingCurrentState[ControllerState](controllerStatesSequence, state~)
	) and state <> state~;

	public pure GetMode: () ==> ControllerMode
	GetMode() == return mode;

end Controller